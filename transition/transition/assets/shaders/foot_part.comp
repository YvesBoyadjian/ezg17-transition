#version 430

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(std430, binding=0) buffer Particles_Pos_In {
	vec4 particles_pos_in[];
};
layout(std430, binding=1) buffer Particles_Vel_In {
	vec4 particles_vel_in[];
};
layout(std430, binding=2) buffer Particles_Col_In {
	vec3 particles_col_in[];
};

layout(std430, binding=3) buffer Particles_Pos_Out {
	vec4 particles_pos_out[];
};
layout(std430, binding=4) buffer Particles_Vel_Out {
	vec4 particles_vel_out[];
};
layout(std430, binding=5) buffer Particles_Col_Out {
	vec3 particles_col_out[];
};


layout (binding=6, offset=0) uniform atomic_uint count;

layout (location=7) uniform uint LastCount;
layout (location=8) uniform uint MaximumCount;
layout (location=9) uniform float dt;

void addParticleToOutputList(vec3 pos, vec4 vel, float ttl, vec3 color);

void main() {
	uint idx = gl_GlobalInvocationID.x + gl_GlobalInvocationID.y * gl_NumWorkGroups.x * gl_WorkGroupSize.x;
	if (idx >= LastCount) return;
	vec4 velocity = particles_vel_in[idx].xyzw;//w is speed
	vec3 position = particles_pos_in[idx].xyz + dt*velocity.xyz*velocity.w;
	float ttl = particles_pos_in[idx].w - dt;
	//velocity = velocity - vec3(0, dt, 0)/10;
	//velocity = /*normalize*/(velocity + (0, 50*dt, 0));
	velocity.y = velocity.y + dt;
	velocity.xyz = normalize(velocity.xyz);
	velocity.w = max(velocity.w - dt/5, 0.01);
	if (ttl > 0) {
		addParticleToOutputList(position, velocity, ttl, particles_col_in[idx]);
	}
}

void addParticleToOutputList(vec3 pos, vec4 vel, float ttl, vec3 color) {
	uint nr = atomicCounterIncrement(count);
	if (nr >= MaximumCount) {
		atomicCounterDecrement(count);
		return;
	}
	particles_pos_out[nr] = vec4(pos, ttl);
	particles_vel_out[nr] = vel;
	particles_col_out[nr] = color;
}